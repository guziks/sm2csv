apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'edu.sc.seis.launch4j'

mainClassName = 'ua.com.elius.sm2csv.Main'

run {
    workingDir = "$project.rootDir/work"
}

run.doFirst {
    workingDir.mkdir()
}

jar {
    manifest {
        attributes("Main-Class": mainClassName)
    }
}

def staticBinaryDestination = file("$rootDir/sbin")

shadowJar {
    baseName = rootProject.name
    classifier = ''
    destinationDir = staticBinaryDestination
}

launch4j {
    outfile = rootProject.name + '.exe'
    headerType = 'console'
    chdir = ''
    mainClassName = project.mainClassName
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar = file("${staticBinaryDestination}/${project.tasks.shadowJar.archiveName}")
    productName = rootProject.name
    description = 'SoMachine to CSV Tags Converter'
    copyright = 'Elius-M'
}

task sbin(type: Copy, dependsOn: 'launch4j', group: 'build') {
    from (project.tasks.launch4j.outputs) {
        include '*.exe'
    }
    into staticBinaryDestination
}

clean {
    delete staticBinaryDestination
}

test {
    workingDir = "$project.rootDir"
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'org.apache.commons:commons-csv:1.2'
    compile 'commons-io:commons-io:2.4'
    compile 'net.sf.jopt-simple:jopt-simple:5.0.3'
}

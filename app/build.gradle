apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'ua.com.elius.sm2csv.Main'

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'edu.sc.seis.launch4j'

//compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

run {
    workingDir = "$project.rootDir/work"
}

run.doFirst {
    workingDir.mkdir()
}

jar {
    manifest {
        attributes("Main-Class": mainClassName)
    }
}

def staticBinaryDestination = file("$rootDir/sbin")
def launcherDir = file("$rootDir/launcher")

shadowJar {
    baseName = rootProject.name
    classifier = ''
    destinationDir = staticBinaryDestination
}

createExe {
    outfile = rootProject.name + '.exe'
    headerType = 'console'
    chdir = ''
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar = file("${staticBinaryDestination}/${project.tasks.shadowJar.archiveName}")
    productName = rootProject.name
    fileDescription = 'SoMachine to CSV Tags Converter'
    copyright = 'ELIUS-M'
}

task sbin(type: Copy, dependsOn: 'createExe', group: 'build') {
    from (project.tasks.createExe.outputs) {
        include '*.exe'
    }
    from (launcherDir)

    into staticBinaryDestination
}

clean {
    delete staticBinaryDestination
}

test {
    workingDir = "$project.rootDir"
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'org.apache.commons:commons-csv:1.5'
    compile 'commons-io:commons-io:2.5'
    compile 'net.sf.jopt-simple:jopt-simple:5.0.4'
    compile 'com.google.guava:guava:25.0-jre'
    compile 'com.esotericsoftware.yamlbeans:yamlbeans:1.13'
    compile 'me.tatarka.parsnip:parsnip:0.3'
}
